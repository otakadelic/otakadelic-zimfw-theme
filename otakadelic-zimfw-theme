# File: otakadelic.zsh-theme

autoload -Uz add-zsh-hook colors vcs_info
colors

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr '!'
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:git*' formats "%{${fg[cyan]}%}[%{${fg[blue]}%}%b%{${fg[yellow]}%}%m%u%c%{${fg[cyan]}%}]%{$reset_color%}"

_otakadelic_virtual_env_prompt() {
    if [[ -n "$VIRTUAL_ENV" ]]; then
        local venv_name="${VIRTUAL_ENV:t}"
        if [[ "$venv_name" == ".venv" ]]; then
            venv_name="${VIRTUAL_ENV:h:t}"
        fi
        VENV_PROMPT="%F{magenta}(${venv_name})%f "
    else
        VENV_PROMPT=""
    fi
}

_otakadelic_precmd() {
    vcs_info
    _otakadelic_virtual_env_prompt
}

add-zsh-hook precmd _otakadelic_precmd
setopt prompt_subst

local user_color="%(!.%B%F{red}.%B%F{blue})"

# Notice we use PS1 and RPS1 directly here, just like asciiship
PS1="%(?..%B%F{red}%? %f%b)${user_color}%n%f%b%B@%b%B%F{yellow}%m%f%b \${VENV_PROMPT}\${vcs_info_msg_0_}%F{green}%# %f"
RPS1="%(?..%B%F{red}:(%f%b )%B%F{yellow}%40<..<%d%<< %f%b"

#
# how to use this theme(zimfw only)
# 
# add one line prompt part of .zimrc
#
#  zmobule otakadelic-zimfw-theme
# 